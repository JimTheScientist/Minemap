plugins {
    id 'java'
    id "application"
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id 'edu.sc.seis.launch4j' version '2.5.0'
}


java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    jcenter()

    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https://maven.latticg.com/"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

// this task replace the @VERSION@ token with the current project.version
task processSource(type: Sync) {
    from sourceSets.main.java
    inputs.property 'version', version
    filter(ReplaceTokens, tokens: [VERSION: version])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:deprecation', '-Xlint:unchecked']
    options.fork = true
}

run {
    args "--no-update"
}

dependencies {
    implementation('com.github.KaptainWutax:MathUtils:5531c4a87b0f1bb85d1dab2bdd18ce375400626a') { transitive = false }
    implementation('com.github.KaptainWutax:SeedUtils:b6a383113ce5d8d09a59e91b28ff064fb97c0709') { transitive = false }
    implementation('com.github.KaptainWutax:MCUtils:f12f3a4d33a70f4f30a9389290adf5a7ac302a8c') { transitive = false }
    implementation('com.github.KaptainWutax:NoiseUtils:a3954932b15a889aa51c82176942d62f291c3b12') { transitive = false }

    implementation('com.github.KaptainWutax:BiomeUtils:31754afc78f2d54c3915b3c31705f9aaf1d18e64') { transitive = false }
    implementation('com.github.KaptainWutax:TerrainUtils:dbba2ba61a2c8920b48b1fb9a32fa8e9fe353d61') { transitive = false }
    implementation('com.github.KaptainWutax:FeatureUtils:61931addbb3a41e92078aef8e3430d22af96e8f6') { transitive = false }

    implementation('com.github.KaptainWutax:ChunkRandomReversal:6b76fb5cf2cd438de56e6a46cea2a83985831834') { transitive = false }
    implementation('com.seedfinding:latticg:1.05')

    implementation('com.google.code.gson:gson:2.8.6')
    implementation('com.formdev:flatlaf:0.38')
    implementation('org.swinglabs.swingx:swingx-core:1.6.5-1')
    implementation('org.pushing-pixels:radiance-trident:3.5.1')
    implementation('one.util:streamex:0.7.3')

}

application {
    mainClass.set("kaptainwutax.minemap.MineMap")
}

shadowJar {
    // this needs to be removed once https://github.com/johnrengelman/shadow/issues/663 is solved
    archiveFileName = "${project.name}-${project.version}.jar"
}

launch4j {
    outfile = "${project.name}-${project.version}.exe"
    icon = "${projectDir}/icon.ico"
    outputDir = "libs"
    supportUrl = "https://github.com/hube12/MineMap/issues"
    jvmOptions = ["-Xms1G", "-Xmx8G"]
    initialHeapPercent = 5
    maxHeapPercent = 80
    jarTask = project.tasks.shadowJar
    productName = "MineMap"
    fileDescription = "A lightning fast map viewer for Minecraft."
    copyConfigurable = [] // don't copy lib
    libraryDir= "ignore_libs"
}

task("release"){
    dependsOn(shadowJar)
    dependsOn(createExe)
}
